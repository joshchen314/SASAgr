FILENAME A99 '...\0_DATA\099CENSUS\L1584';
libname		 AGR  '...\1_lib\';
/* 2014 THESIS */
/* 所得 經營管理者年齡與教育 僱用人數 主要經營種類*/
/*       處理 2010年 資料      */
DATA AGR099_HE_RAW1202;
INFILE A99 LRECL=1584;  /*---------99年農牧業------------*//*-----------------099-----------------*/
INPUT
@1 HUKEY199 $13. /*統一編號*/
@1 hukey299 $12.
@1 C00000199 2. /*鄉鎮市區代號 */
@5 C00000299 3. /*村里代號*/
@8 C00000399 2. /*鄰*/
@10 C00000499 $3. /*連續編號*/
@13 C00000599 1. /*表號*/
@23 C01010199 2. /*一、經營組織型態為何?*/
@27 C03110199 1. /*(一)性別*/
@28 C03210199 2. /*(二)出生年次*/
@30 C03310199 1. /*(三)教育程度*/
@31 C03000199 2. /*三、99年全年主要經營農牧業種類為何?*/
@46 C05100199 1. /*（一）休閒農業主要經營類型*/
@52 C06100199 1. /*1.自行加工*/
@55 C07100199 3. /*1. 常僱員工男*/
@58 C07100299 3. /*2. 常僱員工女*/
@61 C07200199 3. /*1. 臨時員工男*/
@64 C07200299 3. /*2. 臨時員工女*/
@67 C07310199 3. /*1. 不支薪資人員男*/
@70 C07310299 3. /*2. 不支薪資人員女*/
@73 C08100199 7. /*1. 農畜產品銷售收入*/
@83 C08200199 7. /*2. 自行加工農畜產品銷售收入*/
@90 C08300199 7. /*3. 委外加工農畜產品銷售收入*/
@97 C08400199 7. /*4. 休閒農業服務收入*/
@245 C10100199 6. /*（一）年底可耕作地總面積*/
@250   A1                1.@;
 DO I=1 TO 25;
  	   IF I=1 THEN ZA=0; 	   IF I=1 THEN ZA1=0;
           INPUT        C101011 2. /*①可耕作地連續編號*/
                              C102011 6. /*②面積*/
                              C103011 $4. /*③所在地區*/
                              C104011 1. /*④所有權屬*/
                              C105011 1. @; /*⑤主要利用目的*/
		  IF C101011>0 THEN  ZA1=ZA1+1;
		 IF ZA1>1 THEN ZA=1;
   END;
/*農牧業收入*/
   IF C08100199>20 THEN ZB=1; ELSE ZB=0; 	RUN;
 PROC SORT;BY HUKEY199;
/*農牧業畜禽建檔*/
DATA ANI99;
  INFILE A99 LRECL=1584;
INPUT
@1 HUKEY199 $13. /*統一編號*/
@1407   A1                1.@;
 DO I=1 TO 15;
   	   IF I=1 THEN ZC=0; 	   IF I=1 THEN ZC1=0;
                    INPUT      C121011 2.  /*①畜禽名稱代號*/
                                       C122011 1.  /*②契約飼養*/
                                       C123011 6. @; /*③年底飼養數量*/
	      IF C121011>0 THEN DO;
				IF C121011 IN (14,15,16,20) THEN ZC1=ZC1+1;
				ELSE IF C121011 IN (11,12,17,18,19,43) THEN ZC1=ZC1+1;
				ELSE IF C121011 IN (31,32,33,34,35,36,37,38,39,40,41,21) THEN ZC1=ZC1+1;
	     END;
		 IF ZC1>1 THEN ZC=1;
   END;
 PROC SORT;BY HUKEY199;
 DATA AGR099_HE_RAW1202  ;/*-------------MERGE 新的村里 MERGE----------------------新的村里-------------MERGE 新的村里 MERGE-----------------------*/
    MERGE AGR099_HE_RAW1202(IN=A)  ANI99(IN=B);
	BY HUKEY199;
	IF A;		run;

DATA AGR.AGR099_HE_RAW1202(KEEP=y_icm x1_age x2_edu x3_sex x4_pr x5_lnd x6_pl ZA ZB ZC YNANO X7_AGE X8_EDU X9_AGEEDU X10_EDU Y_YY);
SET AGR099_HE_RAW1202;
/*       扣除沒有經營：未從事農牧業生產、沒有加工、沒有休閒      */
if C03000199=17 & C05100199=7 & (C06100199+ C08200199=0) then delete; /*刪除未從事的家數*/
/*       扣除政府機關相關單位、學校、宗教團體*/
IF C01010199 IN (1,2,3,4,5,10,11,12,13) THEN DELETE;
/*       進一步處理 2010年 產出關心的變數      */
/*---------AGE----------*/
if C00000599=1 then C02010199=(99-C03210199)-15;
/*---------地區----------*/
	   if C00000199 in ('65','63','17','18','02','03','04') then hs99=1;
	   if C00000199 in ('66','05','07','08','09') then hs99=2;
	   if C00000199 in ('67','64','20','10','13','16','71','72') then hs99=3;
	   if C00000199 in ('14','15') then hs99=4;
/*---------收入----------*/
IF C08100199=. THEN C08100199=0;
IF C08200199=. THEN C08200199=0;
IF C08300199=. THEN C08300199=0;
IF C08400199=. THEN C08400199=0;
sales=C08100199;
manu=C08200199+C08300199;
rec=C08400199;
if sales=. then sales=0;if manu=. then manu=0;if rec=. then rec=0;
INCOME99=sales+manu+rec;
IF sales=0 THEN YNANO=1;
/*---------僱工----------*/
PL99= C07100199+  C07100299+ C07200199+ C07200299+ C07310199+ C07310299;  
WK_1=C07100199+C07100299;
WK_2=C07200199+C07200299 ;
WK_3=C07310199+C07310299;
WK_TT=WK_1+WK_2+WK_3;
/*---------AGE----------*/
C03210199_a=99-C03210199;
if 15<=C03210199_a<=44 then age99=1;
if 45<=C03210199_a<=64 then age99=2;
if 65<=C03210199_a then age99=3;
/*---------主要經營種類----------*/
if C03000199 in (1,2 ) then pr_ck=1;
else if C03000199 in (3,7 ) then pr_ck=2;
else if C03000199 in (4,5,6 )  then pr_ck=3;
else if C03000199 in (8,9 )  then pr_ck=4;
else if C03000199 in (10,11,12 )  then pr_ck=5;
else if C03000199 in (13,14,15,16 )  then pr_ck=6;
else pr_ck=7;
/*---------VARIABLES LIST----------*/
state=HUKEY299;
year=2010;
y_icm=INCOME99;
Y_YY=SQRT(y_icm);
x1_age=C03210199_a;
x2_edu=C03310199;
x3_sex=C03110199;
x4_pr=pr_ck;
x5_lnd=C10100199/100;
x6_pl=PL99;	
X7_AGE=x1_age*x1_age;
X8_EDU=x2_edu*x2_edu;
X9_AGEEDU=x1_age*x2_edu;
X10_EDU=x2_edu*x4_pr*x6_pl;
/*
ZA;
ZB;
ZC;
*/
PROC SUMMARY DATA=AGR.AGR099_HE_RAW1202;/*不支薪資人員*/
   VAR YNANO;
 OUTPUT OUT=AGRYNANO  SUM=YNANO;
 run;
PROC REG data=AGR.AGR099_HE_RAW1202;
MODEL y_icm = x1_age x2_edu x3_sex x4_pr x5_lnd x6_pl ZA ZB ZC X7_AGE X8_EDU X9_AGEEDU X10_EDU;
OUTPUT OUT=outres
RESIDUAL=res PREDICTED=Yhat;
QUIT;

PROC UNIVARIATE data=outres plot normal;		/*檢測此變項是否為常態分佈*/
VAR res;
TITLE 'Tests for Normality of Residuals';
QUIT;



PROC REG data=AGR.AGR099_HE_RAW1202;
MODEL Y_YY = x1_age x2_edu x3_sex x4_pr x5_lnd x6_pl ZA ZB ZC X7_AGE X8_EDU X9_AGEEDU X10_EDU;
OUTPUT OUT=outres22
RESIDUAL=res22 PREDICTED=Yhat;
QUIT;

PROC UNIVARIATE data=outres22 plot normal;		/*檢測此變項是否為常態分佈*/
VAR res22;
TITLE 'Tests for Normality of Residuals22';
QUIT;

PROC REG data=AGR.AGR099_HE_RAW1202;
MODEL Y_YY = x1_age x2_edu x3_sex x4_pr x5_lnd x6_pl X7_AGE X8_EDU X9_AGEEDU X10_EDU;
OUTPUT OUT=outres33
RESIDUAL=res33 PREDICTED=Yhat;
QUIT;

PROC UNIVARIATE data=outres33 plot normal;		/*檢測此變項是否為常態分佈*/
VAR res33;
TITLE 'Tests for Normality of Residuals33';
QUIT;
